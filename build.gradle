import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

//======================================================================
// PROJECT
//======================================================================

version = '1.0-SNAPSHOT'
group = 'com.sony.ebs.octopus3'

//======================================================================
// REPOSITORIES
//======================================================================

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://mvn.sony-europe.com/content/groups/global" }
        mavenCentral()
        maven { url "http://oss.jfrog.org/repo" }
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.7-SNAPSHOT"
        classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.7.1"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.0.2"
    }
}

repositories {
    mavenLocal()
    maven { url "https://mvn.sony-europe.com/content/groups/global" }
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://oss.jfrog.org/repo" }
    maven { url "http://repo.springsource.org/repo" }
}

//======================================================================
// PLUGINS
//======================================================================

apply plugin: "ratpack-groovy"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "nexus"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'maven-publish'

//======================================================================
// CONFIGURATIONS
//======================================================================

ext.javaLanguageLevel = 1.7

sourceCompatibility = "$javaLanguageLevel"
targetCompatibility = "$javaLanguageLevel"
defaultTasks 'clean', 'build'

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
    all {
        exclude module: "groovy"
    }
}

idea {
    project {
        jdkName "1.7"
        languageLevel "1.7"
    }
    module {
        inheritOutputDirs = false
        outputDir = compileGroovy.destinationDir
        testOutputDir = compileTestGroovy.destinationDir

    }
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

//run { systemProperty('storage.root', System.getProperty('storage.root') ?: System.getProperty('java.io.tmpdir')) }
run { systemProperty('environment', System.getProperty('environment')) }
//test { systemProperty('storage.root', System.getProperty('storage.root') ?: System.getProperty('java.io.tmpdir')) }
test { systemProperty('environment', System.getProperty('environment')) }

//======================================================================
// PACKAGING SHADOWJAR
// 1) Upload snapshots: gradle publishShadowPublicationToSnapshotsRepository
// 2) Upload releases: gradle publishShadowPublicationToReleasesRepository
//======================================================================

shadowJar {
    baseName = "${project.name}-app"
    classifier = group
    transform(AppendingTransformer) {
        resource = "META-INF/spring.handlers"
    }
    transform(AppendingTransformer) {
        resource = "META-INF/spring.schemas"
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = "${project.name}-app"
        }
    }
    repositories {
        maven {
            credentials {
                username project.getProperty('nexusUsername')
                password project.getProperty('nexusPassword')
            }
            name = "releases"
            url = "https://mvn.sony-europe.com/content/repositories/releases"
        }
        maven {
            credentials {
                username project.getProperty('nexusUsername')
                password project.getProperty('nexusPassword')
            }
            name = "snapshots"
            url = "https://mvn.sony-europe.com/content/repositories/snapshots"
        }
    }
}

//======================================================================
// DEPENDENCIES
//======================================================================

dependencies {
    compile "com.sony.ebs.octopus3:octopus3-commons:1.1-SNAPSHOT"
    compile "com.sony.ebs.octopus3:octopus3-commons-ratpack:1.0-SNAPSHOT"

    compile "org.springframework:spring-core:4.0.5.RELEASE"
    compile "org.springframework:spring-beans:4.0.5.RELEASE"
    compile "org.springframework:spring-context:4.0.5.RELEASE"
    testCompile "org.springframework:spring-test:4.0.5.RELEASE"

    compile "io.ratpack:ratpack-rx:0.9.7-SNAPSHOT"
    compile "io.ratpack:ratpack-jackson:0.9.7-SNAPSHOT"

    compile "com.github.jengelman.gradle.plugins:shadow:1.0.2"

    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.5"
    compile group: "org.slf4j", name: "jcl-over-slf4j", version: "1.7.5"
    compile group: "org.slf4j", name: "log4j-over-slf4j", version: "1.7.5"
    compile group: "ch.qos.logback", name: "logback-core", version: "1.1.2"
    compile group: "ch.qos.logback", name: "logback-classic", version: "1.1.2"

    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.0', {
        exclude module: "commons-logging"
        exclude module: "xml-apis"
        exclude module: "groovy"
    }
    compile "com.amazonaws:aws-java-sdk:1.2.9"

    springloaded "org.springsource.springloaded:springloaded-core:1.1.4"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }
    testCompile 'junit:junit:4.11'
    testCompile 'info.cukes:cucumber-junit:1.1.7'
    testCompile 'info.cukes:cucumber-groovy:1.1.7'

    /*
    In order to work around a really flagrant bug ( http://issues.gradle.org/browse/GRADLE-732 )
    You have to create a configuration that includes the jar.archivePath (the jar.archivePath is created by the java plugin)
    and give it the same name as the cucumberRuntime configuration, or name it something different and have the cucumberRuntime
    configuration extend from it as well. VERY ANNOYING BUG!
    */
    cucumberRuntime files("${jar.archivePath}")
}

//======================================================================
// TASKS
// Usage: gradle cucumber
//======================================================================

task cucumber(type: JavaExec) {
    dependsOn assemble
    main = "cucumber.api.cli.Main"
    classpath = files(configurations.cucumberRuntime, sourceSets.test.runtimeClasspath)
    args = ['-f', 'pretty', '--glue', 'src/test/groovy', 'src/test/resources/cukes']
    systemProperty('storage.root', System.getProperty('storage.root') ?: System.getProperty('java.io.tmpdir'))
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.0
}

//======================================================================
// DEPLOY ARTIFACTS (SOURCE, JAVADOC, JAR)
// Usage: gradle upload
//======================================================================

nexus {
    attachSources = true
    attachTests = false
    attachJavadoc = true
    sign = false
    repositoryUrl = "https://mvn.sony-europe.com/content/repositories/releases"
    snapshotRepositoryUrl = "https://mvn.sony-europe.com/content/repositories/snapshots"
}
